# List music albums which are indexed in LocalSearch.
#
# See https://gnome.pages.gitlab.gnome.org/tinysparql/nmm-ontology.html for details on the
# Nepomuk MultiMedia properties.

SELECT
    ?artist_name
    ?album_name
    ?file AS ?first_file
    COUNT(?track) AS ?track_count
    SUM(nie:byteSize(?file)) / (1024.0 * 1024.0) AS ?size_mb
    SUM(nfo:duration(?track)) AS ?duration
FROM tracker:Audio
FROM tracker:FileSystem
{
    ?album a nmm:MusicAlbum ;
        dc:title ?album_name ;
        nmm:albumArtist ?album_artist .

    ?album_artist nmm:artistName ?artist_name .

    ?track nmm:musicAlbum ?album ;
        nie:isStoredAs ?file .
}
GROUP BY ?album
ORDER BY ?artist_name ?album_name
